apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
//apply plugin: 'org.jetbrains.dokka'

ext {
    PUBLISH_GROUP_ID = 'com.apphud'
    PUBLISH_VERSION = project.properties.sdkVersion
    PUBLISH_VERSION_LOCAL = '0.0.0-local'
    PUBLISH_ARTIFACT_ID = 'ApphudSDK-Android'
    PUBLISH_DESCRIPTION = 'Apphud Android SDK is a lightweight open-source library to manage auto-renewable subscriptions and other in-app purchases in your Android app. No backend required.'
    PUBLISH_URL = 'https://github.com/apphud/ApphudSDK-Android'
    PUBLISH_LICENSE_NAME = 'MIT'
    PUBLISH_LICENSE_URL = 'https://github.com/apphud/ApphudSDK-Android/'
    PUBLISH_DEVELOPER_ID = 'apphud'
    PUBLISH_DEVELOPER_NAME = 'Apphud'
    PUBLISH_DEVELOPER_EMAIL = 'hi@apphud.com'
    PUBLISH_SCM_CONNECTION = 'scm:git:github.com:apphud/ApphudSDK-Android.git'
    PUBLISH_SCM_DEVELOPER_CONNECTION = 'scm:git:ssh://github.com:apphud/ApphudSDK-Android.git'
    PUBLISH_SCM_URL = 'https://github.com/apphud/ApphudSDK-Android/tree/master'
}

// Conditional publishing: JitPack by default, Maven Central with -PmavenCentral
def isMavenCentralBuild = project.hasProperty('mavenCentral')
def isPublishToMavenLocal = gradle.startParameter.taskNames.any { it.contains('ToMavenLocal') }
def isJitPackBuild = !isMavenCentralBuild && !isPublishToMavenLocal

// Apply full publishing script for Maven Central and mavenLocal
if (isMavenCentralBuild || isPublishToMavenLocal) {
    apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
}

android {
    defaultConfig {
        minSdkVersion 26
        targetSdk 33
        compileSdk 33
        versionName findProperty('sdkVersion') ?: '0.0.0'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    testOptions {
        unitTests {
            all {
                it.useJUnit()
            }
            returnDefaultValues = true
        }
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            buildConfigField("String","VERSION_NAME","\"${findProperty('sdkVersion') ?: '0.0.0'}\"")
        }
        release {
            buildConfigField("String","VERSION_NAME","\"${findProperty('sdkVersion') ?: '0.0.0'}\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    namespace 'com.apphud.sdk'

    publishing {
        singleVariant('release')
    }
}

// JitPack publishing configuration (default)
if (isJitPackBuild) {
    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    groupId PUBLISH_GROUP_ID
                    artifactId PUBLISH_ARTIFACT_ID
                    // Use local version for mavenLocal, otherwise use regular version
                    version gradle.startParameter.taskNames.any { it.contains('ToMavenLocal') } ? PUBLISH_VERSION_LOCAL : PUBLISH_VERSION

                    from components.release
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation libs.kotlin.stdlib
    implementation libs.billing
    implementation libs.gson

    implementation libs.okhttp
    implementation libs.logging.interceptor
    implementation(libs.kotlinx.coroutines.android)
    implementation libs.play.services.appset
    implementation(libs.lifecycle.process)
    implementation(libs.lifecycle.viewmodel.ktx)
    implementation(libs.lifecycle.runtime.ktx)
    implementation(libs.appcompat)

    testImplementation libs.junit
    testImplementation libs.mockk
    testImplementation libs.kotlinx.coroutines.test
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

// Configure JVM 17 toolchain for unit tests to support MockK
kotlin {
    jvmToolchain(17)
}
