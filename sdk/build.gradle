apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
//apply plugin: 'org.jetbrains.dokka'

ext {
    PUBLISH_GROUP_ID = 'com.apphud'
    PUBLISH_VERSION = project.properties.sdkVersion
    PUBLISH_ARTIFACT_ID = 'ApphudSDK-Android'
    PUBLISH_DESCRIPTION = 'Apphud Android SDK is a lightweight open-source library to manage auto-renewable subscriptions and other in-app purchases in your Android app. No backend required.'
    PUBLISH_URL = 'https://github.com/apphud/ApphudSDK-Android'
    PUBLISH_LICENSE_NAME = 'MIT'
    PUBLISH_LICENSE_URL = 'https://github.com/apphud/ApphudSDK-Android/'
    PUBLISH_DEVELOPER_ID = 'apphud'
    PUBLISH_DEVELOPER_NAME = 'Apphud'
    PUBLISH_DEVELOPER_EMAIL = 'hi@apphud.com'
    PUBLISH_SCM_CONNECTION = 'scm:git:github.com:apphud/ApphudSDK-Android.git'
    PUBLISH_SCM_DEVELOPER_CONNECTION = 'scm:git:ssh://github.com:apphud/ApphudSDK-Android.git'
    PUBLISH_SCM_URL = 'https://github.com/apphud/ApphudSDK-Android/tree/master'
}

// Conditional publishing: JitPack by default, Maven Central with -PmavenCentral
def isMavenCentralBuild = project.hasProperty('mavenCentral')
def isJitPackBuild = !isMavenCentralBuild

// Apply full publishing script only for Maven Central
if (isMavenCentralBuild) {
    apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
}

android {
    defaultConfig {
        minSdkVersion 26
        targetSdk 33
        compileSdk 33
        versionName project.properties.sdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            buildConfigField("String","VERSION_NAME","\"${project.properties.sdkVersion}\"")
        }
        release {
            buildConfigField("String","VERSION_NAME","\"${project.properties.sdkVersion}\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'com.apphud.sdk'

    publishing {
        singleVariant('release')
    }
}

// JitPack publishing configuration (default)
if (isJitPackBuild) {
    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.release
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation libs.kotlin.stdlib
    implementation libs.billing
    implementation libs.gson

    implementation libs.okhttp
    implementation libs.logging.interceptor
    implementation(libs.kotlinx.coroutines.android)
    implementation libs.play.services.appset
    implementation(libs.lifecycle.process)
    implementation(libs.lifecycle.viewmodel.ktx)
    implementation(libs.lifecycle.runtime.ktx)
    implementation(libs.appcompat)

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}
