apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

ext {
    PUBLISH_GROUP_ID = 'com.apphud'
    PUBLISH_VERSION = project.properties.sdkVersion
    PUBLISH_ARTIFACT_ID = 'ApphudSDK-Android'
    PUBLISH_DESCRIPTION = 'Apphud Android SDK is a lightweight open-source library to manage auto-renewable subscriptions and other in-app purchases in your Android app. No backend required.'
    PUBLISH_URL = 'https://github.com/apphud/ApphudSDK-Android'
    PUBLISH_LICENSE_NAME = 'MIT'
    PUBLISH_LICENSE_URL = 'https://github.com/apphud/ApphudSDK-Android/'
    PUBLISH_DEVELOPER_ID = 'apphud'
    PUBLISH_DEVELOPER_NAME = 'Apphud'
    PUBLISH_DEVELOPER_EMAIL = 'hi@apphud.com'
    PUBLISH_SCM_CONNECTION = 'scm:git:github.com:apphud/ApphudSDK-Android.git'
    PUBLISH_SCM_DEVELOPER_CONNECTION = 'scm:git:ssh://github.com:apphud/ApphudSDK-Android.git'
    PUBLISH_SCM_URL = 'https://github.com/apphud/ApphudSDK-Android/tree/master'
}

//apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
    defaultConfig {
        minSdkVersion 26
        targetSdk 33
        compileSdk 33
        versionName project.properties.sdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            buildConfigField("String","VERSION_NAME","\"${project.properties.sdkVersion}\"")
        }
        release {
            buildConfigField("String","VERSION_NAME","\"${project.properties.sdkVersion}\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'com.apphud.sdk'

    publishing {
        singleVariant('release')
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.android.billingclient:billing:6.1.0'
    implementation "com.google.code.gson:gson:2.10.1"

    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3")
    implementation 'com.google.android.gms:play-services-appset:16.0.2'
    implementation("androidx.lifecycle:lifecycle-process:2.6.2")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}